/**
 ** pwm_lib library
 ** Copyright (C) 2015, 2016
 **
 **   Antonio C. Domínguez Brito <adominguez@iusiani.ulpgc.es>
 **     División de Robótica y Oceanografía Computacional <www.roc.siani.es>
 **     and Departamento de Informática y Sistemas <www.dis.ulpgc.es>
 **     Universidad de Las Palmas de Gran  Canaria (ULPGC) <www.ulpgc.es>
 **  
 ** This file is part of the pwm_lib library.
 ** The pwm_lib library is free software: you can redistribute it and/or modify
 ** it under  the  terms of  the GNU  General  Public  License  as  published  by
 ** the  Free Software Foundation, either  version  3  of  the  License,  or  any
 ** later version.
 ** 
 ** The  pwm_lib library is distributed in the hope that  it  will  be  useful,
 ** but   WITHOUT   ANY WARRANTY;   without   even   the  implied   warranty   of
 ** MERCHANTABILITY or FITNESS FOR A PARTICULAR  PURPOSE.  See  the  GNU  General
 ** Public License for more details.
 ** 
 ** You should have received a copy  (COPYING file) of  the  GNU  General  Public
 ** License along with the pwm_lib library.
 ** If not, see: <http://www.gnu.org/licenses/>.
 **/
/*
 * File: changing_period_test.ino 
 * Description: This is a basic example illustrating the use of li-
 * brary pwm_lib. It generates a PWM signal with dynamicaclly changes
 * its period.
 * Date: September 28th, 2016
 * Author: Antonio C. Dominguez-Brito <adominguez@iusiani.ulpgc.es>
 * ROC-SIANI - Universidad de Las Palmas de Gran Canaria - Spain
 */

#include "pwm_lib.h"
#include "tc_lib.h"

using namespace arduino_due::pwm_lib;

#define PWM_PERIOD_PIN_35 100000 // hundredth of usecs (1e-8 secs)
#define PWM_DUTY_PIN_35 1000 // 10 usecs in hundredth of usecs (1e-8 secs)

#define CAPTURE_TIME_WINDOW 40000 // usecs
#define DUTY_KEEPING_TIME 1000 // msecs

// defining pwm object using pin 35, pin PC3 mapped to pin 35 on the DUE
// this object uses PWM channel 0
pwm<pwm_pin::PWMH0_PC3> pwm_pin35;

// To measure PWM signals generated by the previous pwm objects, we will use
// capture objects of tc_lib library as "oscilloscopes" probes. So we will 
// use capture object capture_tc0 for measueing the PWM signal on pin 35.
// IMPORTANT: Take into account that for TC0 (TC0 and channel 0) the TIOA0 is
// PB25, which is pin 2 for Arduino DUE, so capture_tc0's capture pin is pin
// 2. For the correspondence between all TIOA inputs for the 
// different TC module channels, you should consult uC Atmel ATSAM3X8E 
// datasheet in section "36. Timer Counter (TC)"), and the Arduino pin mapping 
// for the DUE.
// All in all, to meausure pwm outputs in this example you should connect the 
// PWM output of pin 35 to object capture_tc0's pin 2.

capture_tc0_declaration(); // TC0 and channel 0
auto& capture_pin2=capture_tc0;

void setup() {
  // put your setup code here, to run once:

  Serial.begin(9600);

  // initialization of capture objects
  capture_pin2.config(CAPTURE_TIME_WINDOW);

  // starting PWM signals
  pwm_pin35.start(PWM_PERIOD_PIN_35,PWM_DUTY_PIN_35);
}

void loop() {
  // put your main code here,to run repeatedly:

  if(!pwm_pin35.set_period(PWM_PERIOD_PIN_35+(PWM_DUTY_PIN_35<<1)))
  {
    Serial.print("[ERROR] set_period(");
    Serial.print(PWM_PERIOD_PIN_35+(PWM_DUTY_PIN_35<<1));
    Serial.println(") failed!");
  }

  delay(DUTY_KEEPING_TIME);

  uint32_t status,duty,period;

  Serial.println("===============================================================");
  status=capture_pin2.get_duty_and_period(duty,period);
  Serial.print("[PIN 35 -> PIN 2] duty: "); 
  Serial.print(
    static_cast<double>(duty)/
    static_cast<double>(capture_pin2.ticks_per_usec()),
    3
  );
  Serial.print(" usecs. period: ");
  Serial.print(period/capture_pin2.ticks_per_usec());
  Serial.println(" usecs.");
  Serial.println("===============================================================");

  if(!pwm_pin35.set_period(PWM_PERIOD_PIN_35+(PWM_DUTY_PIN_35<<3)))
  {
    Serial.print("[ERROR] set_period(");
    Serial.print(PWM_PERIOD_PIN_35+(PWM_DUTY_PIN_35<<3));
    Serial.println(") failed!");
  }

  delay(DUTY_KEEPING_TIME);

  Serial.println("===============================================================");
  status=capture_pin2.get_duty_and_period(duty,period);
  Serial.print("[PIN 35 -> PIN 2] duty: "); 
  Serial.print(
    static_cast<double>(duty)/
    static_cast<double>(capture_pin2.ticks_per_usec()),
    3
  );
  Serial.print(" usecs. period: ");
  Serial.print(period/capture_pin2.ticks_per_usec());
  Serial.println(" usecs.");
  Serial.println("===============================================================");
}

